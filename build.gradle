plugins {
	id 'java'
	id 'maven-publish'
	id 'jacoco'
    id "org.sonarqube" version "3.5.0.2730"
    id 'maven'
    id 'signing'
}

repositories {
    mavenCentral()
}

ext{
	vertxVersion = '4.3.5'
	jupiterVersion = '5.9.1'
	jupiterLauncherVersion = '1.9.1'
	slfApiVersion = '2.0.5'
	logbackVersion = '1.4.5'
}

dependencies {

	implementation "io.vertx:vertx-web:${vertxVersion}"
	
	testImplementation "io.vertx:vertx-config-yaml:${vertxVersion}"
	testImplementation "io.vertx:vertx-junit5:${vertxVersion}"
	testImplementation "io.vertx:vertx-web-client:${vertxVersion}"
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.junit.platform:junit-platform-launcher:${jupiterLauncherVersion}"
	
	testRuntimeOnly "org.slf4j:slf4j-api:${slfApiVersion}"
	testRuntimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

sourceCompatibility='1.8'
targetCompatibility='1.8'

group = "com.romanpierson"
archivesBaseName = "vertx-web-accesslog"
version = "1.5.0-SNAPSHOT"

jar.archiveFileName = "vertx-web-accesslog-1.5.0-SNAPSHOT.jar"

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            groupId 'com.romanpierson'
            artifactId 'vertx-web-accesslog'
            version '1.5.0-SNAPSHOT'
            
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

sonarqube {
    properties {
    	property "sonar.sources", "src/main"
    	property "sonar.tests", "src/test"
        property "sonar.test.exclusions", "**/src/test/**"
    }
}


jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}

wrapper() {
  gradleVersion = '6.9.3'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Vertx Web Accesslog'
        description 'An access log implementation to be used in vert web routes - inspired by apache / tomcat access log configuration'
        packaging 'jar'
        url 'https://github.com/romanpierson/vertx-web-accesslog'

        scm {
          connection 'scm:git@github.com:romanpierson/vertx-web-accesslog.git'
          developerConnection 'scm:git@github.com:romanpierson/vertx-web-accesslog.git'
          url 'https://github.com/romanpierson/vertx-web-accesslog.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'romanpierson'
            name 'Roman Pierson'
            email 'yo@romanpierson.com'
          }
        }
      }
    }
  }
}



